;; Copyright 2019 AI Lab, Vrije Universiteit Brussel - Sony CSL Paris

;; Licensed under the Apache License, Version 2.0 (the "License");
;; you may not use this file except in compliance with the License.
;; You may obtain a copy of the License at

;;     http://www.apache.org/licenses/LICENSE-2.0

;; Unless required by applicable law or agreed to in writing, software
;; distributed under the License is distributed on an "AS IS" BASIS,
;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;; See the License for the specific language governing permissions and
;; limitations under the License.
;;=========================================================================
(in-package :utils)

;; ############################################################################

;; ----------------------------------------------------------------------------
;; configuration:

(defvar *default-id-base-name* "ID")
(defvar *default-var-id-base-name* "?VAR")
(defvar *default-const-id-base-name* "CONST")
(defvar *default-sticker-name* "STICKER")
;; ----------------------------------------------------------------------------
;; private system functionality:

(defparameter *nid-table* (make-hash-table :test #'equal))

(proclaim '(inline get-next-id-number))
(defun get-next-id-number (name)
  "Return the next number to create the next unique id with the given name."
  (declare (type string name))
  (if (gethash name *nid-table*)
      (incf (gethash name *nid-table*))
      (setf (gethash name *nid-table*) 1)))

(proclaim '(inline remove-numeric-tail))
(defun remove-numeric-tail (name)
  (declare (type string name))
  (loop for i from (- (length name) 1) downto 0
        for char = (elt name i)
        when (not (digit-char-p char))
        do (if (equal #\- char)
             (return (subseq name 0 i))
             (return name))
        finally (return name)))

(proclaim '(inline get-base-name))
(defun get-base-name (name &key
                      (remove-numeric-tail t)
                      (remove-question-mark t))
  "Return the base of the given name.
   - If base is a symbol then the base name is the symbol's name.
   - If base is a string then this string is the base name.
   - If remove-question-mark is true and the base name starts with a
     question-mark then this question-mark is removed from the base name.
   - If remove-numeric-tail is true and name is of the form 's-n',
     where s is a string of alphanumerical characters, and n is a string of
     numerical character, then the base is 's', i.e. the hyphen and trailing
     numerical characters are removed."
  (declare (type (or string symbol) name))
  (let* ((name (cond ((stringp name) (upcase name))
                    ((symbolp name) (symbol-name name))
                    (t (write-to-string name))))
        (name-as-string name))
    (if remove-numeric-tail (setq name (remove-numeric-tail name)))
    (if (string= name "") ;; for symbols like -5
      name-as-string
      (if (and remove-question-mark (char-equal #\? (elt name 0)))
        (subseq name 1)
        name))))

;; ----------------------------------------------------------------------------
;; public utilities:

(export '(get-base-name
          make-id
          make-var
          make-kw
          variable-p
          make-const
          reset-id-counters
          string-append
          mkstr
          symb
          internal-symb
          derive-base-name-from-type))

(unless (fboundp 'string-append)
  (defun string-append (&rest strings)
    "concatenates strings"
    (format nil "狺篝蜷铉螬┅ㄤ彐躅黼篝é蝈篝狎珲礤铘螬⒁弭躜铙篝蜷铉泔铘衢铋铉犰狎珲礤铘螽ㄦ矧磲铋狺狎珲礤铘螬ㄤ彐躅簌礅é蝈篝狎珲礤铘螬⑼犭骝弩璎躅轭翦蝾邃簌礅镬鏖翳簌礅镬钺礤黼篝狎珲礤铘螬磲脲簌礅镬ㄦ矧磲铋狺狎珲礤铘螬┅ㄤ彐躅轭翦蝾犰簌礅é蝈篝狎珲礤铘螬⑼犭犷轭翦蝾翳簌礅镬鏖翳簌礅镬钺礤黼篝狎珲礤铘螬ㄩ铘弪ㄦ矧磲铋狺狎珲礤铘螬┅ㄤ彐躅磲脲殇é镳糸镱犰钺礤⒚蝈狒犷蝈趱蝾躅轳蹂铛礅弪邃殇麒殂轶簌礅镬麒矬簌礅镬钺礤泔铙轶趔镦钺礤犷铛礅弪－扉箴黠螂ㄤ邈灬蝈豉疱矧簌礅镬篝蜷铉铛祆钺礤┅戾è忉箦钺礤ㄣ镱è铛祆钺礤溴驷蹯舡殇忉箦钺礤è簌礅镬钺礤簌礅镬钺礤钺礤┅è篝蜷铉钺礤钺礤┅┅磲脲簌礅镬ㄦ矧磲铋豪狺┉幄忉箦钺礤ㄧ弭铄舡殇铛礅弪忉箦钺礤┅┅ㄤ彐躅磲脲鲠é镳糸镱犰钺礤⒚蝈狒犷蝈趱蝾躅轳蹂泼鲠蜷徕戾簌礅镬物翦翳狒殒秕栳鲥翳汨镩沐忮赭邋疳篌轭篝蜷铉矧簌礅镬狍翳狎珲礤铘麸磲脲鲠颥磲脲泔铙矧磲脲殇翳孱疳篌轸翳篝蜷铉涉秕疳篌轸翳簌礅镬翳孱翳轫痨屙孱翎糸镱鏖祆箝眇禊翎脲翳簌礅镬钺礤骝镯轸骢螋桢殓铒蝈翳簌礅镬－扉箴黠螂ㄤ邈灬蝈豉疱矧铛祆篝蜷铉簌礅镬钺礤┅磲脲殇ㄩ钺礤ㄦ矧磲铋⒖幄ㄧ弭忉箦钺礤钺礤┅溴驷蹯舡鲠颦殇忉箦钺礤┅ㄤ彐躅磲脲膑钺礤Ⅳ犭弩篝蜷铉瀹绠堍翦篝堍犷趱蝾轸轭麸簌礅镬轭翦蝾邃轭翳脲黠蜾疳汶徵瀣瀹绠呼弩簪鲠祯弩ㄩ铘弪篝蜷铉躔汜箦钺礤弘妁黠蜾┅ㄤ彐躅鲠蜷徕戾⒃弩麒弭桢轶鲠蜷徕戾楫瀹麒弭桢轸轶簌礅镬镦麒殂翳钺礤篝狎趔鏖翳聃弩糸镱磲螂ㄡ钿簌礅镬ㄥ聃犰ㄣ栳簌礅镬钺礤癌＼咯┅ㄤ彐躅磲脲泔铙é镳糸镱犰钺礤蝈盹鲥铛礤蜷悱翎殪舂⒚蝈狒犷蝈趱蝾躅轳蹂泼泔铙翎铘簌礅镬－扉箴黠螂ㄤ邈灬蝈豉疱矧铛祆篝蜷铉簌礅镬钺礤┅磲脲殇ㄩ钺礤ㄧ弭忉箦钺礤钺礤候屙秭瀛铛礤蜷悱翎殪蝈盹鲥铛礤蜷悱翎殪溴驷蹯舡泔铙舡殇忉箦钺礤┅ㄤ彐躅蝈箦舡殇泔躅翦蝮ī⒁弩弭犰翳泔躅翦蝮骘翳铛礅弪邃殇螽箦翩铋洵翎忪濯磲脲栳箬翎忪呼弩＇羼踽飑舂ㄤ彐躅溴蜷鲥忉箦钺礤骝镯豉疱豉疱⑶轹孱豉疱轭箫礤骘蝽蝈趱蝾篝蜷铉忉箦钺礤ㄣ镱è铛祆豉疱Ⅷè簌礅镬豉疱簌礅镬钺礤豉疱┅è扉篝豉疱ㄤ弪轹瀛忉箦钺礤骝镯豉疱ㄣ狎豉疱┅ㄥ蝌矧⒄铄疱泗邃豉疱徜汜箦麸溴蜷鲥忉箦钺礤骝镯豉疱┅┅换痱镧换蝈箦舡殇泔躅翦蝮换痧蜷铘换祜镳骘鲠蜷徕戾轭换扉篝磲脲簌礅镬⒖骘铫磲脲簌礅镬⒖骘铫磲脲簌礅镬⒖骘锉换磲脲簌礅镬⒖骘锃忉颌磲脲簌礅镬⒖骘锃忉虮换磲脲殇Э忉磲脲殇Э忉换磲脲殇Э骘锃床磲脲殇Э骘锃床┅换泔祆邈ㄣ镱鲠蜷徕戾磲脲鲠鲠蜷徕戾┅┅换换蝈篚祠换换è：科舷：科舷暴ǎ嚎葡：科舷博ǎ嚎葡媳：科舷杯暴换ǎ嚎葡檄铝：科舷铝噎暴ǎ嚎葡檄铝冶：科舷铝冶暴换ǎ嚎铝诃：柯邻畅ǎ嚎铝诃：柯邻穿ǎ嚎葡檄床：科舷床畅换ǎ嚎葡檄床：科舷床穿换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃换篝蜷铉豸殪轸殄蠛换ㄥ痫螋Ж滹黝汜箦躔汜箦篝蜷铉蝈痨徙蝈痨徙瀛汨狎蝈徜骈戾狍篝蜷铉蝈徜躅糸篝蜷铉殒瘐钽趱狒轱瞽蝈盹鲥瘐钽趱狒轱扉箴俱犴屐汜箦汜礤飙汜箦眷轶蝈盹鲥铄黛轭弩蝈盹鲥箴躜轱躞箴徙弩┅ㄤ彐躅瘐钽趱狒轱瞽ㄣ栳颟ㄦ轭汨狎弋缓唷浚ī苘堍┅ㄤ彐躅蝈盹鲥箴躜轱躞箴徙弩箦铘孱沐狍篝蜷铉戾è扉篝蝈痱弩孱翎糸镱箴扉舡箦聃孱沐后痨轸箦聃孱沐＼羽徙箦铘孱沐狍篝蜷铉候屙秭瀛屙痿篚怏羼舂┅ㄦ矧磲铋狺" list-representation)))

(defun remove-punctuation (string)
  "Replace punctuation with spaces in string."
  (substitute-if #\space #'punctuation-p string))

(defun remove-newlines (string)
  "removes newlines inside string"
  (cl-ppcre:regex-replace-all "\\n" string ""))

(defun downcase (str)
  (format nil "(a)" str))

(defun upcase (str)
  (format nil ":@(a)" str))

(defun stringify (symbol-or-string)
  "Make a string from a symbol, number of string, similar to listify"
  (cond ((stringp symbol-or-string)
         symbol-or-string)
        ((numberp symbol-or-string)
         (mkstr symbol-or-string))
        (t
         (symbol-name symbol-or-string))))

(defun string-replace (str1 sub1 sub2)
  "Nondestructively replaces all occurences of sub1 in str1 by sub2"
  (let ((str1 (string str1))
        (str2 "")
        (sub1 (string sub1))
        (sub2 (string sub2))
        (index1 0))
    (loop
       if (string-equal str1 sub1
                        :start1 index1
                        :end1 (min (length str1)
                                   (+ index1 (length sub1))))
       do (setq str2 (concatenate 'string str2 sub2))
         (incf index1 (length sub1))
       else do 
         (setq str2 (concatenate 'string str2
                                 (subseq str1 index1 (1+ index1))))
         (incf index1)
       unless (< index1 (length str1))
       return str2)))

(defun replace-char (string orig replacement)
  "Destructively replaces all occurrences of a character in a string by another one"
  (declare (type string string)
           (type (or character string) orig)
           (type (or character string) replacement))
  ; transform string arguments to characters
  (unless (typep orig 'character)
    (setf orig (character orig)))
  (unless (typep replacement 'character)
    (setf replacement (character replacement)))
  ; maybe there's also a way to do this with a map-like construction
  ; instead of a loop and still be destructive?
  ; using (position orig string :start _) doesn't work because of a bug in ccl
  (loop for i from 0 to (1- (length string))
     when (eq orig (aref string i)) do
       (setf (aref string i) replacement))
  string)

(defun read-file-as-string (pathname)
  (declare (type pathname pathname))
  (let ((os (make-string-output-stream)))
    (with-open-file (is (probe-file pathname))
      (loop for char = (read-char is nil)
            while char
            do (write-char char os)))
    (get-output-stream-string os)))

(defgeneric read-until (string until-part &key start from-end &allow-other-keys)
  (:documentation "Returns part of the string until the until-part is
  encountered."))

(defmethod read-until ((string string) (until-part character) &key (start 0) (from-end nil))
  (subseq string start (position until-part string 
				 :start start :from-end from-end)))

(defmethod read-until ((string string) (until-part string) &key (start 0) (from-end nil))
  (subseq string start (search until-part string 
			       :start1 start :from-end from-end)))

(defun camel-case->lisp (camel-string)
  "Insert - between lowercase and uppercase chars
   and make everything uppercase"
  (declare (string camel-string))
  (let ((*print-pretty* nil))
    (with-output-to-string (result)
      (loop with last-was-lowercase
            for c across camel-string
            when (and last-was-lowercase
                      (upper-case-p c))
            do (princ "-" result)
            if (lower-case-p c)
            do (setf last-was-lowercase t)
            else
            do (setf last-was-lowercase nil)
            do (princ (char-upcase c) result)))))

(defun lisp->camel-case (string &key (from-first t))
  "Remove - between words and make all words uppercase, except the first.
   When from-first is true, the first word is also uppercase."
  (declare (string string))
  (let ((*print-pretty* nil))
    (with-output-to-string (result)
      (loop with last-was-dash
            for c across string
            for i below (length string)
            if (eql c #\-)
            do (setf last-was-dash t)
            else
            do (progn
                 (cond
                  ((and (= i 0) from-first)
                   (princ (char-upcase c) result))
                  (last-was-dash
                   (princ (char-upcase c) result))
                  (t
                   (princ (char-downcase c) result)))
                 (setf last-was-dash nil))))))


;; ############################################################################

(export '(make-random-string split first-word last-word))

(defun make-random-string (&optional (length 30))
  "Generates and returns a random string length LENGTH.  The
string will consist solely of decimal digits and ASCII letters."
  (with-output-to-string (s)
    (dotimes (i length)
      (write-char (ecase (random 5)
                    ((0 1) (code-char (+ #.(char-code #\a) (random 26))))
                    ((2 3) (code-char (+ #.(char-code #\A) (random 26))))
                    ((4) (code-char (+ #.(char-code #\0) (random 10)))))
                  s))))

(defun split (string separator)
  "Splits a string, taking the substrings between the separator.
    E.g. (split 'ab,cd,ef' ',') yields '('ab' 'cd' 'ef')"
  (split-sequence:split-sequence separator string))

(define-compiler-macro split (string separator)
  `(let ((string ,string)
         (separator ,separator))
     (split-sequence:split-sequence separator string)))

(defun first-word (string)
  (let ((index-word-end (search " " string)))
    (subseq string 0 index-word-end)))

(defun last-word (string)
  (let ((index-word-end (search " " string :from-end t)))
    (if index-word-end
      (subseq string (+ 1 index-word-end))
      string)))


;; ############################################################################

(export '(hyphenize))

(defun hyphenize (string-with-spaces)
  (if (find #\space string-with-spaces)
    (string-replace string-with-spaces " " "-")
    string-with-spaces))

;;(hyphenize "the ocean")

;; ############################################################################

(export '(remove-multiple-spaces))

(defun remove-multiple-spaces (string &key (remove-newlines t))
  "Replaces multiple spaces by a single one, including no-break-spaces."
  (if remove-newlines
      (let* ((string-without-newlines (format nil "狺"
                                              (split-sequence:split-sequence #\Newline string :remove-empty-subseqs t)))
             (string-without-no-break-spaces (format nil "狺"
                                                     (split-sequence:split-sequence #+LISPWORKS #\No-Break-Space #+CCL #\No-Break_Space
                                                                                    string-without-newlines :remove-empty-subseqs t))))
        (format nil "狺" (split-sequence:split-sequence #\Space string-without-no-break-spaces :remove-empty-subseqs t)))
      (let ((string-without-no-break-spaces (format nil "狺"
                                                    (split-sequence:split-sequence #+LISPWORKS #\No-Break-Space #+CCL #\No-Break_Space
                                                                                   string :remove-empty-subseqs t))))
        (format nil "狺" (split-sequence:split-sequence #\Space string-without-no-break-spaces :remove-empty-subseqs t)))))


;; ############################################################################

(export '(variablifly))

(defun variablify (symbol &key (package *package*))
  "Turn a symbol into a variable if it isn't one yet."
  (if (variable-p symbol)
    symbol
    (intern (format nil "?a" symbol) package)))